#The first stages of HR, before the trifurcation of this pathway. 
#Coefficients are unknown for me now.
#
#TODO: add KaSim recommended qoafitions instead of 1-s
%var: 'crRNA-search' 1.0
%var: 'Cas9-DNA-cleavage' 1.0
%var: 'MRXunwinding' 1.0
%var: 'MRXcleavage' 1.0
%var: 'MRXfalloff' 1.0
%var: 'MRXbinding' 1.0
%var: 'nucleation' 1.0
%var: 'Rad51-dissociation' 1.0
%var: 'Rad55-Rad57-mediation' 1.0
%var: 'RPA-bind' 10.0
%var: 'Rad51-Rad52-bind' 1.0
%var: 'Rad51-ss-bind' 1.0
%var: 'Rad51-ds-bind' 1.0
%var: 'Rad51-poly' 1.0
%var: 'Rad54-Rad51-binding' 1.0
%var: 'invasion' 1.0
%var: 'chromatin-open' 1.0
%var: 'homology-search' 1.0
%var: 'Rad51-turnover' 1.0
%var: 'DNA-elongation' 1.0
%var: 'DNA-ligation' 1.0
%var: 'Lig-falloff' 1.0
%var: 'Lig-add' 1.0


### CRISPR DNA cleavage ###
   'crRNA complementary DNA search' \
dna(upstream!1, pair!3), dna(downstream!1, pair!4) ,dna(downstream!2, pair!3), dna(upstream!2, pair!4), crRNA(dna, rna!5), crRNA(dna, rna!5) \
-> dna(upstream!1, pair!3), dna(downstream!1, pair!4), dna(downstream!2, pair), dna(upstream!2, pair), crRNA(dna!4, rna!5), crRNA(dna!3, rna!5) \
@ 'crRNA-search'


   'CRISPR-Cas9-mediated complementary DNA cleavage' \
dna(upstream!1, pair!3), dna(downstream!1, pair!4), crRNA(dna!4, rna!5), crRNA(dna!3, cas9!6, rna!5), Cas9(crispr!6) \
-> dna(upstream, pair!3), dna(downstream, pair!4), crRNA(dna!4, rna!5), crRNA(dna!3, cas9!6, rna!5), Cas9(crispr!6) \
@ 'Cas9-DNA-cleavage'


# HDR has 3 main stages
# * Presynapsis
# * Synapsis
# * Postsynapsis
#^:comment "more info about HDR stages"; :in_paper :DSB_repair/heyer2007.pdf; :on_page 11; :from_chunk 0; :to_chunk 16; :from_token 1; :to_token 13 .

#^ http://repairtoire.genesilico.pl/Pathway/3/

### Presynapsis ###

#what is the mechanism of action of MRX?
#add information about Dna2, Sgs2 and Exo1

   'MRX DSB binding' \
dna(upstream, downstream!1, pair!3, binding), dna(upstream!1, pair!4), dna(upstream!2, downstream, pair!3, binding), dna(downstream!2, pair!4, binding), Mre11-Rad50-Xrs2(dna)\
-> dna(upstream, downstream!1, pair!3, binding), dna(upstream!1, pair!4), dna(upstream!2, downstream, pair!3, binding!5), dna(downstream!2, pair!4, binding), Mre11-Rad50-Xrs2(dna!5)\
@ 'MRXbinding'

   'MRX-mediated DNA unwinding' \
dna(upstream, downstream!1, pair!3, binding), dna(upstream!1, pair!4), dna(upstream!2, downstream, pair!3, binding!5), dna(downstream!2, pair!4, binding), Mre11-Rad50-Xrs2(dna!5)\
-> dna(upstream, downstream!1, pair, binding), dna(upstream!1, pair!4), dna(upstream!2, downstream, pair, binding), dna(downstream!2, pair!4, binding!5), Mre11-Rad50-Xrs2(dna!5)\
@ 'MRXunwinding'

   'MRX-mediated DNA cleavage' \
dna(upstream!2, downstream, pair, binding), dna(downstream!2, binding!5), Mre11-Rad50-Xrs2(dna!5)\
-> dna(upstream, downstream, pair, binding), dna(downstream, binding!5), Mre11-Rad50-Xrs2(dna!5)\
@ 'MRXcleavage'

   'MRX falloff' \
Mre11-Rad50-Xrs2(dna!5), dna(binding!5) \
-> Mre11-Rad50-Xrs2(dna), dna(binding) \
@ 'MRXfalloff'

#^ :in_paper DSB_repair/Rad54:%20the%20Swiss%20Army%20knife%20of%20homologous%20recombination.pdf; :on_page 3; :from_chunk 6; :to_chunk 9; :from_token 4; :to_token 6 .
  'RPA binding' \
RPA(dna), dna(pair, binding)\
->  RPA(dna!3), dna(pair, binding!3)\
@ 'RPA-bind'


### Rad51 rules ###
#^:comment "Rad51 pathway"; :image :DSB_repair/rad_51_pathway.jpg

   'Rad51 dsDNA binding' \
dna(pair!4, binding), dna(pair!4, binding), Rad51(dna)  \
-> dna(pair!4, binding), dna(pair!4, binding!2), Rad51(dna!2) \
@ 'Rad51-ds-bind'

#^ :in_paper :DSB_repair/heyer2007.pdf; :on_page 5; :from_chunk 60; :to_chunk 65; :from_token 12; :to_token 4 .
   'Rad51 ssDNA binding' \
dna(pair, binding), Rad51(dna)  \
-> dna(pair, binding!2), Rad51(dna!2) \
@ 'Rad51-ss-bind'
   
   'Rad51 nucleation' \
Rad52(rad51), Rad51(rad52) -> \
Rad52(rad51!6), Rad51(rad52!6) \
@ 'nucleation'

   'Rad51-Rad52 complex binding' \
RPA(dna!3), Rad52(rad51!6), Rad51(rad52!6, dna), dna(pair, binding!3) \
 -> RPA(dna), Rad52(rad51!6), Rad51(rad52!6, dna!3), dna(pair, binding!3) \
@ 'Rad51-Rad52-bind'

   'Rad51 polymerization'  \
 Rad52(rad51!6), Rad51(rad52!6, dna!3, upstream), Rad51(dna, upstream, downstream), dna(upstream!1, pair, binding!3), dna(downstream!1, pair, binding) \
 -> Rad52(rad51!6), Rad51(rad52!6, dna!3,upstream!7), Rad51(dna!5, upstream, downstream!7), dna(upstream!1, pair, binding!3), dna(downstream!1, pair, binding!5) \
@ 'Rad51-poly'
   
   #Rad55-Rad57 should not be binded
   #clarify when Srs2 cleave the Rad51
   'Srs2-regulated dissociation' \
Rad51(dna!1, med), dna(pair, binding!1), Srs2(rad51, rad55-rad57) -> \
Rad51(dna, med!2), dna(pair, binding), Srs2(rad51!2, rad55-rad57) \
@ 'Rad51-dissociation'
#^ :in_paper :DSB_repair/Rad55-Rad57%20paralogs%20functions.pdf; :on_page 4; :from_chunk 0; :to_chunk 1; :from_token 1; :to_token 1 .
#I'm do not sure for the binding Rad55-Rad57 with Rad51. Alex

   'Rad55-Rad57-mediated Srs2 catching' \
Rad55-Rad57(srs2), Srs2(rad55-rad57) \
-> Rad55-Rad57(srs2!1), Srs2(rad55-rad57!1) \
@ 'Rad55-Rad57-mediation'

#^ :in_paper :DSB_repair/heyer2007.pdf; :on_page 19; :from_chunk 5; :to_chunk 5; :from_token 5; :to_token 9 .
#there are unclear here about binding site of Rad54

#^ :in_paper DSB_repair/Rad54:%20the%20Swiss%20Army%20knife%20of%20homologous%20recombination.pdf; :on_page 5; :from_chunk 129; :to_chunk 135; :from_token 5; :to_token 7 .
#here: Rad54 binds to the Rad51 on the ending nucleotide - before the DSB
   'Rad54 binding' \
Rad51(dna!1, med, upstream), dna(upstream, pair, binding!1), Rad54(rad51) \
-> Rad51(dna!1, med!2, upstream), dna(upstream, pair, binding!1), Rad54(rad51!2) \
@ 'Rad54-Rad51-binding'


#Synapsis

#^ :in_paper :DSB_repair/heyer2007.pdf; :on_page 19; :from_chunk 36; :to_chunk 45; :from_token 1; :to_token 13 .
#^ :in_paper :DSB_repair/heyer2007.pdf; :on_page 19; :from_chunk 48; :to_chunk 51; :from_token 2; :to_token 2 .
   'Rad51-mediated homology search' \
Rad51(dna!3,upstream!7), Rad51(dna, upstream, downstream!7, med!6),  Rad54(rad51!6, dna), dna(pair, upstream, binding!3), dna(binding, pair!2,  type~ps), dna(binding, pair!2, chr~closed, type~ps) \
-> Rad51(dna!3,upstream!7), Rad51(dna!4, upstream, downstream!7, med!6), Rad54(rad51!6, dna!5), dna(pair, upstream, binding!3), dna(binding!4, pair!2,  type~ps), dna(binding, pair!2,chr~closed!5, type~ps)\
@ 'homology-search'


#need to determine the role of Rad54 in DNA binding and homology search
#^ :in_paper :DSB_repair/heyer2007.pdf; :on_page 20; :from_chunk 14; :to_chunk 21; :from_token 8; :to_token 3 .


#chromatin open
#are there some chromatin here? We working with genes for transformation, aren't we?
   'Rad54 chromatin open' \
Rad51(dna!3,upstream!7), Rad54(rad51!6, dna!5), Rad51(dna!4, upstream, downstream!7, med!6), dna(pair, upstream, binding!3), dna(binding!4, pair!2, type~ps), dna(binding, pair!2, chr~closed!5, type~ps) \
-> Rad51(dna!3,upstream!7), Rad54(rad51!6, dna!5), Rad51(dna!4, upstream, downstream!7, med!6), dna(pair!2, upstream, binding!3), dna(binding!4, pair!2, type~ps), dna(binding, pair, chr~closed!5, type~ps)\
@ 'chromatin-open'
   
   'Single strand invasion, D-Loop forming' \
Rad51(dna!3,upstream!7), Rad54(rad51!6, dna!5), Rad51(dna!4, upstream, downstream!7, med!6), dna(pair, upstream, chr~opened, binding!3), dna(binding!4, pair!2, chr~opened, type~ps), dna(binding, pair!2, chr~opened!5, type~ps) \
-> Rad51(dna!3,upstream!7), Rad54(rad51!6, dna!5), Rad51(dna!4, upstream, downstream!7, med!6), dna(pair!2, upstream,chr~opened, binding!3), dna(binding!4, pair!2, chr~opened, type~ps), dna(binding, pair, chr~opened!5, type~ps)\
@ 'invasion'

#Postsynapsis (do not ready yet)

 'Rad54-mediated Rad-51-dsDNA turnover' \
Rad54(rad51!6, dna!_), Rad51(dna!3, med!6), dna(pair!2, binding!3), dna(pair!2, type~ps) \
-> Rad54(rad51, dna!6), Rad51(dna, med), dna(pair!2, binding!6), dna(pair!2, type~ps) \
@ 'Rad51-turnover'

#are there DNA elongation at all, or we just bind the plasmid DNA to genome DNA with ligase?

#Add polymerase addition and falloff

  'DNA elongation by DNA-polymerase eta' \
DNAPeta(dna!5, newdna), dna(pair!4, upstream), dna(upstream!2, pair, type~ps, binding!5), dna(downstream!2, pair!4, type~ps) \
-> DNAPeta(dna!5, newdna!6), dna(pair!4, upstream), dna(upstream!2, pair, type~ps, binding!5), dna(downstream!2, pair!4, type~ps), dna(pair, upstream, downstream, binding!6) \
@ 'DNA-elongation'

#Add ligase addition and falloff


   'Ligase addition' \
DNAlig(dna1, dna2), dna(binding, downstream!1), dna(binding, upstream!1) \
-> DNAlig(dna1!6, dna2!7), dna(binding!6, downstream!1), dna(binding!7, upstream!1) \
@ 'Lig-add'

   'DNA ligation' \
DNAlig(dna1!6, dna2!7), dna(pair!4, upstream, binding!7), dna(upstream!2, pair, type~ps, binding), dna(downstream!2, pair!4, type~ps), dna(pair, upstream, downstream, binding!6) \
-> DNAlig(dna1!6, dna2!7), dna(pair!4, upstream!5, binding!7), dna(upstream!2, pair!3, type~ps, binding), dna(downstream!2, pair!4, type~ps), dna(pair!3, upstream, downstream!5, binding!6) \
@ 'DNA-ligation'

   'Ligase falloff' \
DNAlig(dna1!6, dna2!7), dna(binding!6, downstream!1), dna(binding!7, upstream!1) \
-> DNAlig(dna1, dna2), dna(binding, downstream!1), dna(binding, upstream!1) \
@ 'Lig-falloff'
