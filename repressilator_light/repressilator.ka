
#### Signatures

#^ :comment "Repressilator plasmid map"; :image :repressilator_light/plasmid.png
#^ :comment "Microscope video"; :video <https://www.youtube.com/watch?v=HFwSnqYC5LA>

%agent: pLac(binding)
%agent: pTet(binding)
%agent: pL(binding)

%agent: LacI(dna)
%agent: TetR(dna)
%agent: cI(dna)


#### Variables 
%var: 'synth'   0.1
%var: 'binding' 0.1
%var: 'unbinding' 0.0001
%var: 'degrad' 0.001

%var: 'off_rate' 0.1 # per second

%var: 'mod_rate' 1 # per second 

'LacI.Lac' pLac(binding),LacI(dna) <-> pLac(binding!1),LacI(dna!1) @ 'binding' ,'unbinding'
'tetR.synthesis' pLac(binding) -> pLac(binding), TetR() @ 'synth'
'tetR.degrad' TetR() ->  @ 'degrad'

'pL.cI' pL(binding),cI(dna) <-> pL(binding!1),cI(dna!1) @ 'binding' ,'unbinding'
'lacI.synthesis' pL(binding) -> pL(binding),LacI() @ 'synth'
'lacI.degrad' LacI() ->  @ 'degrad'

'pTet.TetR' pTet(binding),TetR(dna) <-> pTet(binding!1),TetR(dna!1) @ 'binding' ,'unbinding'
'cI.synthesis' pTet(binding) -> pTet(binding),cI() @ 'synth'
'cI.degrad' cI() ->  @ 'degrad'

%obs: 'LacI' LacI()
%obs: 'TetR' TetR()
%obs: 'cI' cI()

#%obs: 'pLac<->LacI' pLac(binding!1), LacI(dna!1)
#%obs: 'pTet<->TetR' pTet(binding!1), TetR(dna!1)
#%obs: 'pL<->CI' pL(binding!1), cI(dna!1)

#### Initial conditions 
%init: 25 pLac(binding),pTet(binding),pL(binding)

#%mod: [true] do $FLUX "flux" [true]